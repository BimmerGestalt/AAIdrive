plugins {
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.gladed.androidgitversion' version '0.4.9'
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

// firebase build plugins
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

apply from: 'buildtools/ColoredOutput.gradle'

apply from: 'buildtools/jacoco.gradle'

apply from: 'buildtools/SentryProperties.gradle'

apply from: 'buildtools/checksums.gradle'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "me.hufman.androidautoidrive"
        minSdkVersion 23
        targetSdkVersion 26
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "androidautoidrive-${androidGitVersion.name()}"
    }

    signingConfigs {
        release
    }
    def isRunningOnTravis = System.getenv("CI") == "true"
    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.release.storeFile = file("../keystore.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    flavorDimensions "map", "analytics"

    productFlavors {
        nonalytics {
            dimension "analytics"
        }
        firebase {
            dimension "analytics"
        }
        sentry {
            dimension "analytics"
        }
        nomap {
            dimension "map"
        }
        gmap {
            dimension "map"
            // load api keys from ~/.gradle/gradle.properties
            manifestPlaceholders = [gmaps_api_key:AndroidAutoIdrive_GmapsApiKey]
        }
    }
}

//tasks.matching {it instanceof Test}.all {
//    it.outputs.upToDateWhen {false}
//}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    testImplementation "org.mockito:mockito-core:2.23.4"
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation 'org.awaitility:awaitility-scala:3.1.5'
    androidTestImplementation "org.mockito:mockito-android:2.23.4"
    androidTestImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    androidTestImplementation 'org.awaitility:awaitility-scala:3.1.5'
    implementation project(path: ':IDriveConnectKitAndroid')
    implementation project(path: ':IDriveConnectKit')
    androidTestImplementation project(path: ':IDriveConnectKitAndroid')
    androidTestImplementation project(path: ':IDriveConnectKit')
    androidTestImplementation "org.bouncycastle:bcmail-jdk16:1.46"

    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    gmapImplementation 'com.google.android.gms:play-services-maps:16.0.0'
    gmapImplementation 'com.google.android.gms:play-services-location:16.0.0'
    gmapImplementation 'com.google.android.libraries.places:places:1.1.0'
    gmapImplementation 'com.google.maps:google-maps-services:0.1.20'

    firebaseImplementation 'com.google.firebase:firebase-core:16.0.8'
    firebaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    sentryImplementation 'io.sentry:sentry-android:1.7.16'
    sentryImplementation 'org.slf4j:slf4j-nop:1.7.25'
}

