// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.9.25'
    ext.kotlin_coroutines_version = '1.6.0'
    ext.androidx_lifecycle_extensions_version = '2.2.0'
    ext.androidx_navigation_version = '2.7.7'
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://maven.fabric.io/public'
        }
        maven {
            name = 'ajoberstar-backup'
            url = 'https://ajoberstar.org/bintray-backup/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = "mapbox"
                // Use the secret token you stored in gradle.properties as the password
                password = System.env.AndroidAutoIdrive_MapboxDownloadToken ? System.env.AndroidAutoIdrive_MapboxDownloadToken : AndroidAutoIdrive_MapboxDownloadToken
            }
        }
    }
}

// fix mocking in jdk12+
// https://stackoverflow.com/a/76267308/169035
subprojects{
    tasks.withType(Test).configureEach{
        jvmArgs = jvmArgs + ['--add-opens=java.base/java.lang=ALL-UNNAMED', '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED', '--add-opens=java.base/java.util=ALL-UNNAMED']
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
